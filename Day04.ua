IsDigit ← ∊:+@0⇡10
Split ← ⊜□≠,
Card ← ≡(□ ⊜⋕ IsDigit. °□) Split @| °□ ⊡ 1 Split @:
Winners ← /+ ∊ ∩°□ °⊟
Value ← (ⁿ : 2 - 1|0)=0.

Effects ← ;:⬚0≡(⊂⊙(▽ : 1)▽ :0+1,) :⇡⧻.
Step ← +1⊙(+×⊙:)⊙(⊡).⊙(.)
Run ← ∧(⊂ +× ⊡ ⊃⊃⊃∘(°⊟↙2⋅∘)(⊢ ⋅∘)(↘ 2 ⋅∘)) ⇡⧻⊢.
Cap ← ⊂ ▽:1⊡1△.

PartOne ← /+ ≡(Value Winners Card °□) Split @\n
PartTwo ← /+ ⊢ Run Cap Effects ≡(Winners Card °□) Split @\n

⍤ ⊃⋅∘ ≍ [□ [41 48 83 86 17] □ [83 86 6 31 17 9 48 53]] Card "Card 1: 41 48 83 86 17 | 83 86  6 31 17  9 48 53"
⍤ ⊃⋅∘ ≍ 8 Value Winners Card "Card 1: 41 48 83 86 17 | 83 86  6 31 17  9 48 53"

&fras "day04.txt"
PartOne

&fras "day04.txt"
PartTwo
